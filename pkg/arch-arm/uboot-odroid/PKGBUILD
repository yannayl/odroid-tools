# Overview
# ========
#
#   Package:    U-Boot for ODROID-X/X2/U2/U3
#   Maintainer: Zoltan Tombol <zoltan dot tombol at gmail>
#
#   Based on:
#     - alarm/uboot-odroid (Arch Linux ARM)
#         Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
#

buildarch=4

pkgbase=uboot-odroid
pkgname=('uboot-odroid' 'uboot-odroid-x')
pkgver=2016.09.01
pkgrel=2
arch=('armv7h')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
install="${pkgbase}.install"
backup=('boot/extlinux/extlinux.conf' 'boot/uEnv.txt')
makedepends=('bc' 'dtc')
_commit=dd9a970aa4accf5d266d334c0f319c674e933027
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/bl1.HardKernel"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/bl2.HardKernel"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/tzsw.HardKernel"
        'sd_fusing.sh'
        '0001-generic-distro-config-support.patch'
        '0002-odroid-x-support.patch'
        'extlinux.conf'
        'boot.cmd'
        'uEnv.txt')
md5sums=('61c628f8034477c946e173ed174efeb4'
         '3ab6d3cc2061bc2590d60320254017c6'
         '841502de02bd42f2898e36c89c260b0f'
         'c38faafa02a6a1ae834457f378c82113'
         '15f23e76433b7fb248810881788637ee'
         '69a433f3f190a0bf8256e4fdb2bdf4a5'
         '076a1520581b70f3f0f7541745261751'
         '6d6d735b10a26ffe4abeee0301485f72'
         'b9b0db17fe7f0a0deef71d15d46e3dfd'
         'f61dc7bc098c80133962619b02227f74')

prepare() {
  cd "u-boot-${pkgver}"

  patch -p1 -i "${srcdir}/0001-generic-distro-config-support.patch"
}

build() {
  cd "u-boot-${pkgver}"

  unset CFLAGS
  unset CXXFLAGS
  unset CPPFLAGS

  # Build for X2/U2/U3.
  make distclean 
  make odroid_config
  make EXTRAVERSION="-${pkgrel}"
  mv u-boot-dtb.bin "${srcdir}/u-boot-dtb.bin"

  # Patch and build for X.
  patch -p1 -i "${srcdir}/0002-odroid-x-support.patch"
  make distclean
  make odroid_config
  make EXTRAVERSION="-${pkgrel}"

  # Generate loadable script.
  ./tools/mkimage -A arm -T script -n 'Fallback boot script' \
                  -d "${srcdir}/boot.cmd" "${srcdir}/boot.scr"
}

package_uboot-odroid() {
  pkgdesc='U-Boot for ODROID-X2/U2/U3'

  cd "${srcdir}"

  mkdir -p "${pkgdir}/boot"
  cp u-boot-dtb.bin "${pkgdir}/boot/u-boot.bin"
  cp {{bl1,bl2,tzsw}.HardKernel,sd_fusing.sh} "${pkgdir}/boot"

  mkdir -p "${pkgdir}/boot/extlinux"
  cp extlinux.conf "${pkgdir}/boot/extlinux"

  cp boot.scr "${pkgdir}/boot"
  cp uEnv.txt "${pkgdir}/boot"
}

package_uboot-odroid-x() {
  pkgdesc='U-Boot for ODROID-X'

  cd "${srcdir}"

  mkdir -p "${pkgdir}/boot"
  cp "u-boot-${pkgver}/u-boot-dtb.bin" "${pkgdir}/boot/u-boot.bin"
  cp {{bl1,bl2,tzsw}.HardKernel,sd_fusing.sh} "${pkgdir}/boot"

  mkdir -p "${pkgdir}/boot/extlinux"
  cp extlinux.conf "${pkgdir}/boot/extlinux"

  cp boot.scr "${pkgdir}/boot"
  cp uEnv.txt "${pkgdir}/boot"
}
