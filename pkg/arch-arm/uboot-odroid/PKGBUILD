# Overview
# ========
#
#   Package:    U-Boot for ODROID-X/X2/U2/U3
#   Maintainer: Zoltan Tombol <zoltan dot tombol at gmail>
#
#   Based on:
#     - alarm/uboot-odroid (Arch Linux ARM)
#         Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
#

buildarch=4

pkgbase=uboot-odroid
pkgname=('uboot-odroid' 'uboot-odroid-x')
pkgver=2017.01
pkgrel=1
arch=('armv7h')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
install="${pkgbase}.install"
backup=('boot/extlinux/extlinux.conf')
makedepends=('bc' 'dtc')
_commit=dd9a970aa4accf5d266d334c0f319c674e933027
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/bl1.HardKernel"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/bl2.HardKernel"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/tzsw.HardKernel"
        'sd_fusing.sh'
        '0001-generic-distro-config-support.patch'
        '0002-odroid-x-support.patch'
        'extlinux.conf')
sha512sums=('d24c7d1305d81326a0f18071c073cd80c5ac43fcc8d8495509c56a8207620acc3d3e86afa873f738953841b3f6273e345493aad449f2464673275cc221270527'
            '04bd91f8c46f72d0f6d09b4cec70e9617e82cccee5e6b660e2ec54aa64972169f48b7c81ee2a9f31fa513bf99021de443bebc871a25569eaaf25aaf4da208e8d'
            'e40cfd4ee603ab954dc1db213fe8f2cfc387a2e3224cfab71bade4cbdf64d89558d5300c113e877c173e7561dcc716f098cf976c5b5a725b9d8462543d802e03'
            '6730b711150f74f6861868d8df31913588018bafe3098f4e51e0226ef53264aa7c73ade19d21b173beb9725aca79422521077df725982a651bcc4e3c9671ef04'
            '2428c31fab908e19b8462db3bc516ec43dad61f8a91b8e04b6d3e1ddba42e4ec5732f85c1a32cbd8d49b5b086858064eba11135c43272195c0f64e04f8d79805'
            '475056bb6afce076145aae7dfbfc5d1bc6fdfba4e983108795c40e3ac1792a5da76b8ea5d88473e56fe4bf3d143003281fdcde1b1aca216de9b02aa8af9447fc'
            'c3fa1e89fa0bed85ddf677d4f807e24ae5c0299d064a87d2068bc273f518a049edbf264d91bcbba34c69dc9017ae053436d26fe59db043b85066d9290cf0b23d'
            'd62530d051ccef62679ca29b351113b095f5e5728cd320d44cb729a4fb03f73d798c7428a4aef532a0f67a5f5cb9ce649b078776cd238b4709ac16e9ef5a3480')

prepare() {
  cd "u-boot-${pkgver}"

  patch -p1 -i "${srcdir}/0001-generic-distro-config-support.patch"
}

build() {
  cd "u-boot-${pkgver}"

  unset CFLAGS
  unset CXXFLAGS
  unset CPPFLAGS

  # Build for X2/U2/U3.
  make distclean 
  make odroid_config
  make EXTRAVERSION="-${pkgrel}"
  mv u-boot-dtb.bin "${srcdir}/u-boot-dtb.bin"

  # Patch and build for X.
  patch -p1 -i "${srcdir}/0002-odroid-x-support.patch"
  make distclean
  make odroid_config
  make EXTRAVERSION="-${pkgrel}"
}

package_uboot-odroid() {
  pkgdesc='U-Boot for ODROID-X2/U2/U3'

  cd "${srcdir}"

  mkdir -p "${pkgdir}/boot"
  cp u-boot-dtb.bin "${pkgdir}/boot/u-boot.bin"
  cp {{bl1,bl2,tzsw}.HardKernel,sd_fusing.sh} "${pkgdir}/boot"

  mkdir -p "${pkgdir}/boot/extlinux"
  cp extlinux.conf "${pkgdir}/boot/extlinux"
}

package_uboot-odroid-x() {
  pkgdesc='U-Boot for ODROID-X'

  cd "${srcdir}"

  mkdir -p "${pkgdir}/boot"
  cp "u-boot-${pkgver}/u-boot-dtb.bin" "${pkgdir}/boot/u-boot.bin"
  cp {{bl1,bl2,tzsw}.HardKernel,sd_fusing.sh} "${pkgdir}/boot"

  mkdir -p "${pkgdir}/boot/extlinux"
  cp extlinux.conf "${pkgdir}/boot/extlinux"
}
